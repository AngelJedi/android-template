buildscript {
    ext.dbtoolsVersion = '10.0.1'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta2'
        classpath "org.dbtools:gradle-dbtools-plugin:$dbtoolsVersion"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0' // version plugin support
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

apply plugin: 'com.android.application'
apply plugin: 'dbtools'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions' // ./gradlew dependencyUpdates -Drevision=release

// Manifest version information
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def versionMajorMinorPatchBuild = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
def buildTime = new Date().getTime()

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
}

ext {
    // versions for libraries that multiple dependencies
    supportLibVersion = '26.0.1'
    daggerVersion = '2.11'
    loganSquareVersion = '1.3.7'
    okhttpVersion = '3.8.1'
    pocketKnifeVersion = '3.2.1'
    pocketBusVersion = '1.2.0'
    butterKnifeVersion = '8.8.1'
}

dependencies {
    // Android
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:preference-v14:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:percent:${supportLibVersion}"
    implementation "com.android.support:support-annotations:${supportLibVersion}" // includes support-v4
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.google.android.gms:play-services-analytics:11.2.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // UI
    implementation "com.vikingsen:pocketknife-core:${pocketKnifeVersion}"
    annotationProcessor "com.vikingsen:pocketknife-compiler:${pocketKnifeVersion}"
    implementation "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

    // Code
    implementation 'org.threeten:threetenbp:1.3.6'
    implementation "org.dbtools:dbtools-android:$dbtoolsVersion"
    implementation 'org.apache.commons:commons-lang3:3.1' // max version should not exceed 3.1 in order to support Xiaomi devices
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.devbrackets.android:recyclerext:2.1.1'
    implementation 'com.evernote:android-job:1.1.11'
    implementation "com.vikingsen:pocketbus:$pocketBusVersion"
    annotationProcessor "com.vikingsen:pocketbus-compiler:$pocketBusVersion"
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Network
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    // JSON Parsing
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'

    // Dagger 2
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Custom SQLite databasethreetenbp
//    compile 'org.sqlite:sqlite-android:3.8.9.0-20150415'
//    compile 'net.sqlcipher:sqlcipher-aar:3.1.0.20140823'

    // Test (Integration)
    // Espresso core dependencies
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.0'
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1' // fix conflicting issue with jsr305 annotations
    androidTestImplementation "com.android.support:support-annotations:${supportLibVersion}" // fix dependency conflict warning

    // Espresso contrib dependencies
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.0'
    androidTestImplementation "com.android.support:appcompat-v7:${supportLibVersion}"
    androidTestImplementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    androidTestImplementation "com.android.support:design:${supportLibVersion}"

    // Test (Unit)
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.47'
    testImplementation 'org.threeten:threetenbp:1.3.6' // required for DBTools java junit tests
    testImplementation 'org.xerial:sqlite-jdbc:3.20.0'
    testAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName versionMajorMinorPatchBuild
        buildConfigField "String", "USER_AGENT_APP_NAME", "\"AndroidTemplate\""
        buildConfigField "String", "ANALYTICS_KEY", "\"${getAnalyticsKey()}\""

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true // do not generate png's for svg files

        // Espresso
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }

    // set dummy signing values if not defined in ~/.gradle/gradle.properties
    if (!project.hasProperty('myKeystore')) {
        println "Using dummy signing values"
        project.ext.setProperty('myKeystore', 'dummy')
        project.ext.setProperty('myKeystorePassword', 'dummy')
        project.ext.setProperty('myKeyAlias', 'dummy')
        project.ext.setProperty('myKeyPassword', 'dummy')
    }

    // defined values my* in ~/.gradle/gradle.properties
    signingConfigs {
        prodConfig {
            storeFile file(myKeystore)
            storePassword myKeystorePassword
            keyAlias myKeyAlias
            keyPassword myKeyPassword
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-DEV'
            applicationIdSuffix '.dev'
            buildConfigField "long", "BUILD_TIME", "0l" // to improve build times, do allow change on every build
        }
        alpha {
            initWith(buildTypes.release)
            versionNameSuffix '-ALPHA'
            applicationIdSuffix '.dev'
            buildConfigField "long", "BUILD_TIME", "${buildTime}l"
            debuggable true
            signingConfig signingConfigs.prodConfig
        }
        beta {
            initWith(buildTypes.release)
            versionNameSuffix '-BETA'
            buildConfigField "long", "BUILD_TIME", "${buildTime}l"
            signingConfig signingConfigs.prodConfig
        }
        release {
            buildConfigField "long", "BUILD_TIME", "${buildTime}l"
            versionNameSuffix ''
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.prodConfig
        }
    }

    sourceSets {
        // add dagger generated files (works only with debug build)
//        test.java.srcDirs += ['build/generated/source/apt/test/debug']
    }
}

// Regenerate DBTools Database classes: ./gradlew dbtools-genclasses
dbtools {
    type 'ANDROID'

    basePackageName 'org.jdc.template.model.database'
    outputSrcDir 'src/main/java/org/jdc/template/model/database'

    injectionSupport true
    jsr305Support true
    includeDatabaseNameInPackage true
    dateType 'JSR-310'
    sqlQueryBuilderSupport false
    rxJavaSupport true
}

// ===== TEST TASKS =====

// show JUnit failure reason in console (the following may fail tests run from IDE)
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

// Define coverage source.
// If you have rs/aidl etc... add them here.
def coverageSourceDirs = [
        'src/main/java',
]

// Rename and place "myAnalyticsKey" in global gradle.properties
def getAnalyticsKey() {
    return hasProperty('myAnalyticsKey') ? myAnalyticsKey : ''
}
