buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
        classpath 'org.dbtools:dbtools-gen:1.4.+'
    }
}

apply plugin: 'android'
apply plugin: 'sonar-runner'

archivesBaseName = 'android-template'
def version = '1.0.0'
def androidVersionCode = 1

configurations {
    provided
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Android
    compile "com.android.support:appcompat-v7:19+" // includes support-v4

    // Code
    compile 'com.squareup.dagger:dagger:1.2.0'
    provided 'com.squareup.dagger:dagger-compiler:1.2.0'
    compile 'com.jakewharton:butterknife:4.0.1'
    compile 'joda-time:joda-time:2.3'
    compile 'org.dbtools:dbtools-android:1.4.0'

    // Test
//    instrumentTestCompile 'com.google.android.apps.common.testing:espresso:1.0-SNAPSHOT:bundled'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}

// support "provided" dependency (dependencies that are only used during compile-time)
android.applicationVariants.all {
    variant -> variant.javaCompile.classpath += configurations.provided
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.1'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19

        versionCode androidVersionCode
        versionName version // use the gradle version

//        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    // defined values my* in ~/.gradle/gradle.properties
//    signingConfigs {
//        prodConfig {
//            storeFile file(myKeystore)
//            storePassword myKeystorePassword
//            keyAlias myKeyAlias
//            keyPassword myKeyPassword
//        }
//    }

    buildTypes {
        debug {
            versionNameSuffix '-DEV'
        }
        release {
//            signingConfig signingConfigs.prodConfig;
            versionNameSuffix ''
        }
    }

    // set the build timestamp on the file and in the app
    android.applicationVariants.all { variant ->

        // set the build timestamp
        def timestamp = new Date().format("yyyyMMdd-HHmm", TimeZone.getTimeZone("GMT"));

        // name the file with the version and timestamp (does not work well with the IDE... do not do on debug build)
        if (variant.buildType.name != "debug" && variant.zipAlign) {
            def apk = variant.outputFile;
            def newName = apk.name.replace(".apk", "-" + version + "-" + timestamp + ".apk");
            variant.outputFile = new File(apk.parentFile, newName);
        }

        variant.mergeResources.doLast {
            def buildNumber = variant.buildType.name != "release" ? version + variant.buildType.versionNameSuffix + " (" + timestamp + ")" : version + " (" + timestamp + ")";
            File valuesFile = file("${buildDir}/res/all/${variant.dirName}/values/values.xml")
            String content = valuesFile.getText('UTF-8')
            content = content.replace("devBuild", buildNumber)
            valuesFile.write(content, 'UTF-8')
        }
    }
}

sonarRunner {
    sonarProperties {
        if (!project.hasProperty('mySonarHostUrl')) {
            println '!!!! Sonar properties must be set in your gradle.properties !!!!'
            return
        }

        // Project Information
        property 'sonar.projectKey', 'AndroidTemplate'
        property 'sonar.projectName', 'Android Template'
        property 'sonar.projectVersion', version

        property 'sonar.links.homepage', 'https://github.com/jeffdcamp/android-template'
        property 'sonar.links.issue', 'https://github.com/jeffdcamp/android-template/issues'
        property 'sonar.links.scm', 'https://github.com/jeffdcamp/android-template.git'

        // Sonar Information (defined in gradle.properties)
        property "sonar.host.url", mySonarHostUrl
        property "sonar.jdbc.url", mySonarJdbcUrl
        property "sonar.jdbc.driverClassName", mySonarJdbcDriver
        property "sonar.jdbc.username", mySonarUsername
        property "sonar.jdbc.password", mySonarPassword


        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
        properties["sonar.tests"] = android.sourceSets.instrumentTest.java.srcDirs
        properties["sonar.binaries"] = [file("build/classes/debug"), file("build/classes/alpha")]
    }
}

task dbtools {
    description = 'Generate DBTools domain classes'
    doLast {
        // NOTE: BE Sure to add the following above:
        //    buildscript {
        //        repositories {
        //            mavenCentral()
        //        }
        //        dependencies {
        //            classpath 'org.dbtools:dbtools-gen:1.+'
        //        }
        //    }

        System.out.println("Generating DBTools Classes...")

        // properties
        String schemaFilename = "src/main/database/schema.xml";
        boolean injectionSupport = true; // support for CDI
        boolean dateTimeSupport = true; // support for jsr DateTime (Joda Time)
        String baseOutputDir = "src/main/java/org/company/project/domain";
        String basePackageName = "org.company.project.domain";
        org.dbtools.gen.android.AndroidDBObjectBuilder.buildAll(schemaFilename, baseOutputDir, basePackageName, injectionSupport, dateTimeSupport);
    }
}
