buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter() // version plugin support
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0-rc3'
        classpath ('org.dbtools:dbtools-gen:3.0.0') { /*changing=true*/ }
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.7' // version plugin support
    }
}

apply plugin: 'com.android.application'
apply plugin: 'sonar-runner'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions' // ./gradlew dependencyUpdates -Drevision=release

archivesBaseName = 'android-template'

def artOnly = false; // enable a fast build supported ONLY by Android 5.0+

// Manifest version information
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def versionMajorMinorPatch = "${versionMajor}.${versionMinor}.${versionPatch}"
def versionMajorMinorPatchBuild = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Android
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.android.support:support-annotations:21.0.3' // includes support-v4
    compile 'com.android.support:multidex:1.0.1'

    // Code
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    compile 'com.vikingsen:pocketknife-core:1.0.1'
    provided 'com.vikingsen:pocketknife-compiler:1.0.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup:otto:1.3.6'
    compile 'joda-time:joda-time:2.7'
    compile 'org.dbtools:dbtools-android:3.0.0'
    compile 'org.dbtools:dbtools-query:2.1.1'
    compile 'com.melnykov:floatingactionbutton:1.2.0'

    // Custom SQLite database
//    compile 'org.sqlite:sqlite-android:3.8.8.1-20150126'
//    compile 'net.sqlcipher:sqlcipher-aar:3.1.0.20140823'

    // Test (Integration)
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.0') {exclude module: 'jsr305'}
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
//    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    // Test (Unit)
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.3-beta'

}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 21

        if (artOnly) {
            minSdkVersion 21 // temp enable to perform faster builds
        }

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName versionMajorMinorPatch
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        multiDexEnabled true

        // Enable the experimental Jack build tools. (note... may not work with shrinking enabled)
        // useJack = true

        // Espresso
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

//    jacoco {
//        version '0.6.2.201302030002'
//    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    // defined values my* in ~/.gradle/gradle.properties
//    signingConfigs {
//        prodConfig {
//            storeFile file(myKeystore)
//            storePassword myKeystorePassword
//            keyAlias myKeyAlias
//            keyPassword myKeyPassword
//        }
//    }

    buildTypes {
        debug {
            versionNameSuffix '-DEV'
            buildConfigField "String", "VERSION_NAME_FULL", "\"${versionMajorMinorPatchBuild}-DEV\""
            // does not work with dagger (https://code.google.com/p/android/issues/detail?id=69174&thanks=69174&ts=1398894143)
//            testCoverageEnabled true
        }
        release {
//            signingConfig signingConfigs.prodConfig;
            buildConfigField "String", "VERSION_NAME_FULL", "\"${versionMajorMinorPatchBuild}\""
            versionNameSuffix ''
            minifyEnabled true
            shrinkResources true
        }
    }

    // set the build timestamp on the file and in the app
    android.applicationVariants.all { variant ->
        // set the build timestamp
        def timestamp = new Date().format("yyyyMMdd-HHmm", TimeZone.getTimeZone("GMT"));

        // name the file with the version and timestamp (does not work well with the IDE... do not do on debug build)
        if (variant.buildType.name != "debug" && variant.outputs.zipAlign) {
            def apk = variant.outputs.get(0).outputFile;
            def newName = apk.name.replace(".apk", "-" + versionCode + "-" + timestamp + ".apk");
            variant.outputs.get(0).outputFile = new File(apk.parentFile, newName);
        }
    }
}

sonarRunner {
    sonarProperties {
        if (!project.hasProperty('mySonarHostUrl')) {
            println '!!!! Sonar properties must be set in your gradle.properties !!!!'
            return
        }

        // Project Information
        property 'sonar.projectKey', 'AndroidTemplate'
        property 'sonar.projectName', 'Android Template'
        property "sonar.projectVersion", versionMajorMinorPatchBuild

        property 'sonar.links.homepage', 'https://github.com/jeffdcamp/android-template'
        property 'sonar.links.issue', 'https://github.com/jeffdcamp/android-template/issues'
        property 'sonar.links.scm', 'https://github.com/jeffdcamp/android-template.git'

        // Sonar Information (defined in gradle.properties)
        property "sonar.host.url", mySonarHostUrl
        property "sonar.jdbc.url", mySonarJdbcUrl
        property "sonar.jdbc.driverClassName", mySonarJdbcDriver
        property "sonar.jdbc.username", mySonarUsername
        property "sonar.jdbc.password", mySonarPassword


        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
        properties["sonar.tests"] = android.sourceSets.androidTest.java.srcDirs
        if (file("build/intermediates/classes/debug").exists()) {
            properties["sonar.binaries"] = [file("build/intermediates/classes/debug")]
        }
        if (file("build/intermediates/classes/alpha").exists()) {
            properties["sonar.binaries"] = [file("build/intermediates/classes/alpha")]
        }
    }
}

task dbtools {
    description = 'Generate DBTools domain classes'
    doLast {
        // NOTE: BE Sure to add the following above:
        //    buildscript {
        //        repositories {
        //            mavenCentral()
        //        }
        //        dependencies {
        //            classpath 'org.dbtools:dbtools-gen:2.+'
        //        }
        //    }

        System.out.println("Generating DBTools Classes...")

        org.dbtools.gen.android.AndroidObjectsBuilder builder = new org.dbtools.gen.android.AndroidObjectsBuilder();

        builder.setXmlFilename("src/main/database/schema.xml");
        builder.setOutputBaseDir("src/main/java/org/company/project/domain");
        builder.setPackageBase("org.company.project.domain");

        org.dbtools.gen.GenConfig genConfig = new org.dbtools.gen.GenConfig();
        genConfig.setInjectionSupport(true);
        genConfig.setJsr305Support(true);
        genConfig.setDateTimeSupport(true);
        genConfig.setIncludeDatabaseNameInPackage(true);
        genConfig.setOttoSupport(true);

        builder.setGenConfig(genConfig);
        builder.build();
    }
}

// ===== TEST TASKS =====

// show JUnit failure reason in console (the following may fail tests run from IDE)
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

// Define coverage source.
// If you have rs/aidl etc... add them here.
def coverageSourceDirs = [
        'src/main/java',
]

task testDebugJacoco(type: JacocoReport, dependsOn: "testDebug") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class'
            ])
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("$buildDir/jacoco/testDebug.exec")
    // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
    // We iterate through the compiled .class tree and rename $$ to $.
    doFirst {
        new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}
