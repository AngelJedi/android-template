buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter() // version plugin support
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.3'
        classpath ('org.dbtools:dbtools-gen:2.2.1') { /*changing=true*/ }
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.5' // version plugin support
    }
}

apply plugin: 'com.android.application'
apply plugin: 'sonar-runner'
apply plugin: 'findbugs'
apply plugin: 'com.github.ben-manes.versions' // ./gradlew dependencyUpdates -Drevision=release

archivesBaseName = 'android-template'
// Manifest version information
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0
def versionMajorMinorPatch = "${versionMajor}.${versionMinor}.${versionPatch}"
def versionMajorMinorPatchBuild = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Android
    compile "com.android.support:appcompat-v7:20.0.0" // includes support-v4
    compile "com.android.support:support-annotations:20.0.0"

    // Code
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.squareup:otto:1.3.5'
    compile 'joda-time:joda-time:2.5'
    compile ('org.dbtools:dbtools-android:2.2.1') { /*changing=true*/ }
//    compile ('org.dbtools:dbtools-query:2.0.0-SNAPSHOT') { changing=true }

    androidTestCompile('com.jakewharton.espresso:espresso:1.1-r3') {
        exclude group: 'com.squareup.dagger'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}


def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 20
    buildToolsVersion '20'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 20

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName versionMajorMinorPatch
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

//    jacoco {
//        version '0.6.2.201302030002'
//    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }

    // defined values my* in ~/.gradle/gradle.properties
//    signingConfigs {
//        prodConfig {
//            storeFile file(myKeystore)
//            storePassword myKeystorePassword
//            keyAlias myKeyAlias
//            keyPassword myKeyPassword
//        }
//    }

    buildTypes {
        debug {
            versionNameSuffix '-DEV'
            buildConfigField "String", "VERSION_NAME_FULL", "\"${versionMajorMinorPatchBuild}-DEV\""
            // does not work with dagger (https://code.google.com/p/android/issues/detail?id=69174&thanks=69174&ts=1398894143)
//            testCoverageEnabled true
        }
        release {
//            signingConfig signingConfigs.prodConfig;
            buildConfigField "String", "VERSION_NAME_FULL", "\"${versionMajorMinorPatchBuild}\""
            versionNameSuffix ''
        }
    }

    // set the build timestamp on the file and in the app
    android.applicationVariants.all { variant ->
        // set the build timestamp
        def timestamp = new Date().format("yyyyMMdd-HHmm", TimeZone.getTimeZone("GMT"));

        // name the file with the version and timestamp (does not work well with the IDE... do not do on debug build)
        if (variant.buildType.name != "debug" && variant.outputs.zipAlign) {
            def apk = variant.outputs.get(0).outputFile;
            def newName = apk.name.replace(".apk", "-" + versionCode + "-" + timestamp + ".apk");
            variant.outputs.get(0).outputFile = new File(apk.parentFile, newName);
        }
    }
}

sonarRunner {
    sonarProperties {
        if (!project.hasProperty('mySonarHostUrl')) {
            println '!!!! Sonar properties must be set in your gradle.properties !!!!'
            return
        }

        // Project Information
        property 'sonar.projectKey', 'AndroidTemplate'
        property 'sonar.projectName', 'Android Template'
        property "sonar.projectVersion", versionMajorMinorPatchBuild

        property 'sonar.links.homepage', 'https://github.com/jeffdcamp/android-template'
        property 'sonar.links.issue', 'https://github.com/jeffdcamp/android-template/issues'
        property 'sonar.links.scm', 'https://github.com/jeffdcamp/android-template.git'

        // Sonar Information (defined in gradle.properties)
        property "sonar.host.url", mySonarHostUrl
        property "sonar.jdbc.url", mySonarJdbcUrl
        property "sonar.jdbc.driverClassName", mySonarJdbcDriver
        property "sonar.jdbc.username", mySonarUsername
        property "sonar.jdbc.password", mySonarPassword


        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
        properties["sonar.tests"] = android.sourceSets.androidTest.java.srcDirs
        properties["sonar.binaries"] = [file("build/intermediates/classes/debug"), file("build/intermediates/classes/alpha")]
    }
}

task dbtools {
    description = 'Generate DBTools domain classes'
    doLast {
        // NOTE: BE Sure to add the following above:
        //    buildscript {
        //        repositories {
        //            mavenCentral()
        //        }
        //        dependencies {
        //            classpath 'org.dbtools:dbtools-gen:2.+'
        //        }
        //    }

        System.out.println("Generating DBTools Classes...")

        org.dbtools.gen.android.AndroidObjectsBuilder builder = new org.dbtools.gen.android.AndroidObjectsBuilder();
        builder.setXmlFilename("src/main/database/schema.xml");
        builder.setOutputBaseDir("src/main/java/org/company/project/domain");
        builder.setPackageBase("org.company.project.domain");
        builder.setInjectionSupport(true);
        builder.setJsr305Support(true);
        builder.setDateTimeSupport(true);
        builder.setEncryptionSupport(false);
        builder.setIncludeDatabaseNameInPackage(true);
        builder.build();
    }
}
